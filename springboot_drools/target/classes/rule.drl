//当前规则文件用于计算个人所得税
package calculation
import com.jobs.entity.Calculation

//计算应纳税所得额
rule "tax_setWagemore"
    salience 100
    //设置生效日期
    date-effective "2022-10-01"
    no-loop true
    when
        $cal:Calculation(wage > 0)
    then
        double wagemore = $cal.getWage() - 5000;
        $cal.setWagemore(wagemore);
        update($cal);
end

//设置税率、速算扣除数
rule "tax_3000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal:Calculation(wagemore <= 3000)
    then
        $cal.setCess(0.03);//税率
        $cal.setPreminus(0);//速算扣除数
        update($cal);
end

rule "tax_12000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal:Calculation(wagemore > 3000 &&wagemore <= 12000)
    then
        $cal.setCess(0.1);//税率
        $cal.setPreminus(210);//速算扣除数
        update($cal);
end


rule "tax_25000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal : Calculation(wagemore > 12000 &&wagemore <= 25000)
    then
        $cal.setCess(0.2);
        $cal.setPreminus(1410);
        update($cal);
end

rule "tax_35000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal : Calculation(wagemore > 25000 &&wagemore <= 35000)
    then
        $cal.setCess(0.25);
        $cal.setPreminus(2660);
        update($cal);
end

rule "tax_55000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal : Calculation(wagemore > 35000 &&wagemore <= 55000)
    then
        $cal.setCess(0.3);
        $cal.setPreminus(4410);
        update($cal);
end

rule "tax_80000"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal : Calculation(wagemore > 55000 &&wagemore <= 80000)
    then
        $cal.setCess(0.35);
        $cal.setPreminus(7160);
        update($cal);
end

rule "tax_max"
    salience 90
    no-loop true
    activation-group "SETCess_Group"
    when
        $cal : Calculation(wagemore > 80000)
    then
        $cal.setCess(0.45);
        $cal.setPreminus(15160);
        update($cal);
end

rule "tax_result"
    salience 80
    when
        $cal : Calculation(wage > 0 && wagemore > 0 && cess > 0)
    then
        //扣税额
        double wageminus = $cal.getWagemore() * $cal.getCess() - $cal.getPreminus();
        double actualwage = $cal.getWage() - wageminus;
        $cal.setWageminus(wageminus);
        $cal.setActualwage(actualwage);
        System.out.println("--税前工资："+$cal.getWage());
        System.out.println("--应纳税所得额："+$cal.getWagemore());
        System.out.println("--税率：" + $cal.getCess());
        System.out.println("--速算扣除数：" + $cal.getPreminus());
        System.out.println("--扣税额：" + $cal.getWageminus());
        System.out.println("--税后工资：" + $cal.getActualwage());
end

//当前规则文件用于计算个人所得税
// 规则文件中包含了以下规则：
// 1. 计算应纳税所得额：当个人工资大于5000时，应纳税所得额为个人工资减去5000；
// 2. 设置税率、速算扣除数：当应纳税所得额小于等于3000时，税率为3%，速算扣除数为0；
// 当应纳税所得额大于3000小于等于12000时，税率为10%，速算扣除数为210；
//当应纳税所得额大于12000小于等于25000时，税率为20%，速算扣除数为1410；
//当应纳税所得额大于25000小于等于35000时，税率为25%，速算扣除数为2660；当应纳税所得额大于35000小于等于
// 35000时，税率为30%，速算扣除数为4410；当应纳税所得额大于35000小于等于55000时，税率为35%，速算扣除数为7160；
//当应纳税所得额大于55000小于等于80000时，税率为45%，速算扣除数为15160；
//当应纳税所得额大于80000时，税率为45%，速算扣除数为15160；
// 4. 计算扣税额、税后工资：计算扣税额时，应纳税所得额乘以税率，减去速算扣除数；计算税后工资时，个人工资减去扣税额。
// 5. 输出结果：输出个人工资、应纳税所得额、税率、速算扣除数、扣税额、税后工资。